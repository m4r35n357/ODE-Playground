
define void output (x, y, z, t) { print x, " ", y, " ", z, " ", t, " _ _ _ 0.0\n" }

define horner (n, j[], h) {
    auto i, s
    for (i = n; i > -1; i--) s = s * h + j[i]
    return s
}
 
define const (v, k) {
    if (k == 0) return v
    return 0.0
}

define abs (u[], k) {
    if (u[0] > 0) return u[k]
    if (u[0] < 0) return - u[k]
    return 1.0 / 0.0
}
 
define mul (u[], v[], k) {
    auto j, p
    for (j = 0; j < k + 1; j++) p += u[j] * v[k - j]
    return p
}
 
define exp (exp[], u[], k) {
    auto j, e
    if (k == 0) return e(u[0])
    for (j = 0; j < k; j++) e += exp[j] * (k - j) * u[k - j]
    return e / k
}

define sin (cos[], u[], k) {
    auto j, s
    if (k == 0) return s(u[0])
    for (j = 0; j < k; j++) s += cos[j] * (k - j) * u[k - j]
    return s / k
}

define cos (sin[], u[], k) {
    auto j, c
    if (k == 0) return c(u[0])
    for (j = 0; j < k; j++) c += sin[j] * (k - j) * u[k - j]
    return - c / k
}
 
define tan (sec2[], u[], k) {
    auto j, t
    if (k == 0) return s(u[0]) / c(u[0])
    for (j = 0; j < k; j++) t += sec2[j] * (k - j) * u[k - j]
    return t / k
}

define sec2 (tan[], u[], k) {
    auto j, s2
    if (k == 0) return 1.0 + (tan[0] * tan[0])
    for (j = 0; j < k; j++) s2 += tan[j] * (k - j) * tan[k - j]
    return 2.0 * s2 / k
}
